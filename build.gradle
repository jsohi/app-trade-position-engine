plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'idea'
}

application {
    mainClass = 'com.bofa.equity.TradePositionEngineApp'
    applicationDefaultJvmArgs = [
        '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
        '--add-opens', 'java.base/jdk.internal.misc=ALL-UNNAMED'
    ]
}

defaultTasks 'clean', 'build', 'test'

ext {
    appGroup          = 'com.bofa.equity'
    appVersion        = '1.0.0-SNAPSHOT'
    javaVersion       = JavaVersion.VERSION_21
    AERON_VERSION     = '1.50.1'
    AGRONA_VERSION    = '2.4.0'
    SBE_VERSION       = '1.37.1'
    DECIMAL_4J_VERSION  = '1.0.3'  // garbage free decimal operations
    HDR_HISTOGRAM_VERSION = '2.2.2' // latency capture library with plotting
    AFFINITY_VERSION    = '3.23.3' // OpenHFT thread-to-core pinning
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    group appGroup
    version appVersion

    compileJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.compilerArgs.addAll(['--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'])
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    compileTestJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.compilerArgs.addAll(['--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'])
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    repositories {
        mavenCentral()
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}


dependencies {
    implementation project(':shared')
    implementation project(':publisher')
    implementation project(':receiver')
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.32'
    implementation group: 'io.aeron', name: 'aeron-all', version: AERON_VERSION
    implementation group: 'org.agrona', name: 'agrona', version: AGRONA_VERSION
    implementation group: 'net.openhft', name: 'affinity', version: AFFINITY_VERSION
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.14.0'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.14.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.14.0'
    testImplementation group: 'io.cucumber', name: 'cucumber-java',                     version: '7.20.1'
    testImplementation group: 'io.cucumber', name: 'cucumber-junit-platform-engine',     version: '7.20.1'
    testImplementation group: 'io.cucumber', name: 'cucumber-picocontainer',             version: '7.20.1'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-suite',        version: '1.14.0'
    testRuntimeOnly    group: 'org.junit.platform', name: 'junit-platform-suite-engine', version: '1.14.0'
    testImplementation group: 'org.hdrhistogram',   name: 'HdrHistogram',                version: HDR_HISTOGRAM_VERSION
}

test {
    useJUnitPlatform()
    jvmArgs('--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED')
    jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')
    jvmArgs('--add-opens', 'java.base/jdk.internal.misc=ALL-UNNAMED')
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}
